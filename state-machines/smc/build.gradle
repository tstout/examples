apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    smc
}

ext.libraries = [
        runtime_core: [
                'com.google.guava:guava:14.0.1'
        ],
        test_core: [
                'org.mockito:mockito-core:1.9.0',
                'junit:junit:4.10'
        ]
]

//
// TODO - look at this plugin for JS support: http://eriwen.github.io/gradle-js-plugin/
//



//
// Use local lib dependency since current SMC not available in maven central (Boo!)
//
dependencies {
    smc fileTree(dir: 'lib', includes: ['*.jar'])
    //smc 'net.sf.smc:smc:4.3'
    compile (
            fileTree(dir: 'lib', includes: ['*.jar']),
            libraries.runtime_core)
    testCompile (libraries.test_core)
}

task gensrcDirs {
    ext.genSrcDir = new File("${project.buildDir}/generated-src/example/smc")
    genSrcDir.mkdirs()
}

// consider using this style of args...
//args = ['-S', 'rake', 'migrate', "RAILS_ENV=${project.getProperty('database')}"]
//
task smcJava(type: JavaExec, dependsOn: 'gensrcDirs') {
    def smcFiles = fileTree('src/main/smc').include('**/*.sm')
    ext.outputPath = "${project.buildDir}/generated-src/example/smc"
    inputs.files smcFiles
    outputs.dir outputPath
    classpath configurations.smc
    main = 'net.sf.smc.Smc'
    args('-java')
    args('-d')
    args("${project.buildDir}/generated-src/example/smc")
    args(smcFiles.files)
}

sourceSets.main.java.srcDir smcJava.outputPath
compileJava.dependsOn smcJava



